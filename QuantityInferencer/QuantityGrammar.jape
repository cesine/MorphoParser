/*
*  This file is designed to annotate approximate and exact quantities for the use in evaluating entailment
*
*  Gina, 17 Aug 2010
*/
Phase:   Quantity
Input: Token Lookup Number NounPackage NP
Options: control = appelt

//////////////////////////////////////////////////////////////
//Number macros based on ANNIE NE transducer 2001
Macro: NUMERICALVALUE
// two hundred and thirty five
// twenty five
//a hundred
//at least half a dozen former Soviet republics
(
	( {Token.string == "a"} | {Token.string == "one"} )?
	{Number}
)

Macro: NOUNCLUSTER
( {NounPackage}|{CollectionNounCluster}
//( {NP}
)

//////////////Qualification of the Quantity//////////////////
Macro: SCOPESHIFTERS
( {Token.string == "no"}|
 {Token.string == "not"}
)
Macro: APPROXIMATEQUANTITY
(	{Token.string == "about"}| 
	{Token.string == "around"}| 
	{Token.string == "many"}|
	{Token.string == "over"}|
	{Token.string == "some"}|
	{Token.string == "several"}|
	{Token.string == "nearly"}|
	{Token.string == "roughly"}|
	{Token.string == "approximately"}|
	{Token.string == "only"}|
	( {Token.string == "at"} {Token.string == "least"})|
	( {Token.string == "more"} {Token.string == "than"})|
	( {Token.string == "up"} {Token.string == "to"})|
	( {Token.string == "a"} {Token.string == "few"})|
	( {Token.string == "as"} {Token.string == "few"} {Token.string == "as"})|
	( {Token.string == "as"} {Token.string == "many"} {Token.string == "as"})
)

Macro: QUANTITYVERBS
( 	{Token.stem == "higher"}|
	{Token.stem == "increase"}|
	{Token.stem == "decreas"}|
	{Token.stem == "widespread"}|
	{Token.stem == "gain"}
)

Macro: COMPARITIVES
//the world's largest drug company.
( 	{Token.stem == "highest"}|
	{Token.stem == "largest"}|
	{Token.stem == "smallest"}|
	{Token.category== "JJS"}
)

//////////////////////////////////////////////////////////////
//Quantity Rules

Rule: ApproximateQuantity
//more than 100 bullets
//at least two charges
//over three children
//over 40 billion dollars
//several rounds of talks
//at least half a dozen former Soviet republics 
//more than 200 tests, more than 200 nuclear tests = 200 tests

(
	(SCOPESHIFTERS)?
	(APPROXIMATEQUANTITY)
	(  (NUMERICALVALUE):quantity  (NOUNCLUSTER):noun  )
 )
:number -->
  :number.ApproximateQuantity = 
  {	rule = "ApproximateQuantity", 
	quantity = :quantity.Number.value, 
	headnoun = :noun.NounPackage.headnoun}



Rule: ExactQuantity
//  100 bullets
//  two charges

(
 (NUMERICALVALUE):quantity  (NOUNCLUSTER):headnoun  
)
:number -->
  :number.ExactQuantity = 
  			{rule = "ExactQuantity", 
			quantity = :quantity.Number.value, 
			headnoun = :headnoun.NounPackage.headnoun}


//////////////////////////////////////////////////////////////
//Age rules
Rule: Age
//35-year-old
//35 years old
// 5-year-olds
//55-year-old 55 years old= 55 year

(
 (NUMERICALVALUE):quantity
 ({Token.string == "-"})?
( ({Token.stem == "year"}{Token.stem == "old"})| ({Token.string == "year-old"}) )
)
:number -->
  :number.ExactQuantity = 
  			{rule = "Age", 
			quantity = :quantity.Number.value, 
			headnoun = "year"}
