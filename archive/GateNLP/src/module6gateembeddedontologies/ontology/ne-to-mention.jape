/*
 * This file is part of a hands-on exercise for the third GATE training course,
 * Montreal, August/September 2010.  It is released under the GNU Lesser
 * General Public Licence version 3.0, a copy of which is available at
 * http://gate.ac.uk/gate/licence.html
 *
 * Original code (c) 2010 The University of Sheffield.
 */
Phase: AddClasses
Input: Location Person Organization
Options: control = all

// Match locations and add a class feature by upper-casing the first character
// of the locType, and create a corresponding Mention annotation.  If the
// location has no locType then we assign it to the top-level Location class.
Rule: Loc
({Location}):loc
-->
:loc {
  for(Annotation l : locAnnots) {
    String locType = (String)l.getFeatures().get("locType");
    if(locType == null) locType = "location";
    l.getFeatures().put("class", ontology.getDefaultNameSpace() +
        locType.substring(0, 1).toUpperCase() + locType.substring(1));
  }
},
:loc.Mention = { class = :loc.Location.class }


// Same idea for organizations
Rule: Org
({Organization}):org
-->
:org {
  for(Annotation o : orgAnnots) {
    String orgType = (String)o.getFeatures().get("orgType");
    if(orgType == null) orgType = "organization";
    o.getFeatures().put("class", ontology.getDefaultNameSpace() +
        orgType.substring(0, 1).toUpperCase() + orgType.substring(1));
  }
},
:org.Mention = { class = :org.Organization.class }
